# DrellYanNoMatchingW.sin 
# Simple complete physics example: DY with WHIZARD shower
########################################################################
#
# Copyright (C) 1999-2022 by 
#     Wolfgang Kilian <kilian@physik.uni-siegen.de>
#     Thorsten Ohl <ohl@physik.uni-wuerzburg.de>
#     Juergen Reuter <juergen.reuter@desy.de>
#     with contributions from
#     cf. main AUTHORS file
#
# WHIZARD is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by 
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# WHIZARD is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
########################################################################

model = SM
alias quark = u:d:s:c
alias antiq = U:D:S:C
process qqee = quark:antiq, quark:antiq => e1, E1

ms = 0
mc = 0

compile

sqrts = 1960 GeV
beams = p, pbar => lhapdf

$title = "Pt of Z"
$x_label = "Pt / GeV"
$y_label = "N"
histogram Pt_distribution (0, 250, 1)

ps_isr_primordial_kt_width = 0
ps_isr_tscalefactor = 1

?ps_fsr_active = true
?ps_isr_active = true
?hadronization_active = true
$shower_method = "WHIZARD"
?ps_isr_pt_ordered = false
?ps_isr_only_onshell_emitted_partons = true
ps_max_n_flavors = 5
ps_mass_cutoff = 0.5

### Rick Field's CDF Tune D6 using CTEQ6L1
$ps_PYTHIA_PYGIVE = "MSTP(5)=108"

?mlm_matching = false

cuts = all M > 80 GeV [combine[e1,E1]]
       and all M < 100 GeV [combine[e1,E1]]

integrate (qqee) { iterations = 2:20000 }

n_events = 500

?rebuild_events = true
$sample = "DrellYanNoMatchingW"
sample_format = lhef

analysis = record Pt_distribution (eval Pt [combine [e1,E1]])

checkpoint = 2500

simulate (qqee)

show (results)
